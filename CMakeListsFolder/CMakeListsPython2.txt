cmake_minimum_required(VERSION 2.8)
project(texpp)

if(APPLE)
    set(CMAKE_MACOSX_RPATH 1)
endif(APPLE)

# Enable testing
include(CTest)

# Find boost libraries
set(Boost_USE_MULTITHREADED ON)
find_package(Boost 1.56.0 COMPONENTS system filesystem regex python REQUIRED)

# Find python interpreter
find_package(PythonInterp 2.7)
if(NOT PYTHONINTERP_FOUND)
    message("python interpreter NOT found - tests/hrefkeywords will NOT be built.")
endif(NOT PYTHONINTERP_FOUND)

find_package( PythonLibs REQUIRED)
if(NOT PYTHONLIBS_FOUND)
    message("python libraries NOT found - texpy and hrefkeywords will NOT be built.")
endif(NOT PYTHONLIBS_FOUND)

# Find Unicode libraries
if(UNIX AND NOT APPLE)
    set(CMAKE_MODULE_PATH ${CMAKE_MODULE_PATH} ${PROJECT_SOURCE_DIR})
    find_package(ICU 4.4 REQUIRED)
endif(UNIX AND NOT APPLE)

# Find tex executable (required for tests/tex and tests/hrefkeywords)
find_program(TEX_EXECUTABLE "tex")
if(TEX_EXECUTABLE STREQUAL "TEX_EXECUTABLE-NOTFOUND")
    message("tex executable NOT found - tests/tex and tests/hrefkeywords will NOT be built.")
else(TEX_EXECUTABLE STREQUAL "TEX_EXECUTABLE-NOTFOUND")
    set(TEX_FOUND ON)
endif(TEX_EXECUTABLE STREQUAL "TEX_EXECUTABLE-NOTFOUND")
mark_as_advanced(TEX_EXECUTABLE)

# Flags
set(CMAKE_CXX_FLAGS "-std=c++11 -Wall -Wextra -Wunused -Woverloaded-virtual -Wwrite-strings")
include_directories(${CMAKE_SOURCE_DIR})

# Subdirectories
add_subdirectory(texpp)

if(Boost_PYTHON_FOUND)
    add_subdirectory(texpy)
    if(UNIX AND NOT APPLE)
        add_subdirectory(hrefkeywords)
    endif(UNIX AND NOT APPLE)
endif(Boost_PYTHON_FOUND)
